# Two-tier Stochastic Frontier model - Exponential Specification - MLE and JLMS measures

## Related to Ch 5 - Boston Public Education application

series Depvar = DepY  #input the dependent variable
list Xlist =  RegrListEdRelInc # input the list of regressors

smpl --no-missing DepY Xlist

ols Depvar Xlist --quiet
matrix bcoef = $coeff

scalar sv = .5
scalar sw = .5
scalar su = .5

set max_verbose full
#This so that the names of the variables appear in the estimation output
string varblnames = varname(Xlist)
string errnames = ",sigmav,sigmaw,sigmau"
string allcoefnames = varblnames ~ errnames

mle logl = check ? -log(sw + su) + log(exp(a1)*cnorm(b1) + exp(a2)*cnorm(b2)):NA
    series res = Depvar - lincomb(Xlist, bcoef)
    # Note: the loglikelihood in the exponential spec does not contain any constant terms

series a1 = sv^2/(2*su^2) + res/su
series b1 = -res/sv - sv/su
series a2 = sv^2/(2*sw^2) - res/sw 
series b2 = res/sv - sv/sw


scalar check = (sv>0) && (sw>0) && (su>0)
params bcoef sv sw su
param_names allcoefnames
end mle   --verbose  --robust
