# Two-tier Stochastic Frontier model - NE model - DepVar in logs


series Depvar = lmaidc  #input the dependent variable - this is from the Ch 4.4 application
list Xlist =  ListUSreg_noCol  # input the list of regressors - this is from the Ch 4.4 application

smpl --no-missing Depvar Xlist

ols Depvar Xlist --quiet
matrix bcoef = $coeff

scalar sv = 1.5
scalar sw = 1.5
scalar su = 1.5

set max_verbose full
#This so that the names of the variables appear in the estimation output
string varblnames = varname(Xlist)
string errnames = ",sigmav,sigmaw,sigmau"
string allcoefnames = varblnames ~ errnames

mle logl = check ? -log(sw + su) + log(exp(a1)*cnorm(b1) + exp(a2)*cnorm(b2)):NA
    series res = Depvar - lincomb(Xlist, bcoef)
    # Note: the loglikelihood in the exponential spec does not contain any constant terms

series a1 = sv^2/(2*su^2) + res/su
series b1 = -res/sv - sv/su
series a2 = sv^2/(2*sw^2) - res/sw 
series b2 = res/sv - sv/sw


scalar check = (sv>0) && (sw>0) && (su>0)
params bcoef sv sw su
param_names allcoefnames
end mle    --verbose  #--robust

series resmle = Depvar - lincomb(Xlist, bcoef)
series a1 = sv^2/(2*su^2) + resmle/su
series b1 = -resmle/sv - sv/su
series a2 = sv^2/(2*sw^2) - resmle/sw 
series b2 = resmle/sv - sv/sw

series loglExp = -ln(sw + su) + ln(exp(a1)*cnorm(b1) + exp(a2)*cnorm(b2))


#Calculating conditional measures for the Semi-log Exp spec

series chi1 = cnorm(b2) + exp(a1-a2)*cnorm(b1)
series chi2 = exp(a2-a1)*chi1
scalar lambda = 1/sw + 1/su

#conditional expectations needed to compute 2TSF metrics when the dep var is in logs
series Exp_w_pos = (lambda/(lambda-1))*(1/chi2)*(cnorm(b1)+exp(0.5*((b2 + sv)^2 - b1^2))*cnorm(b2+sv))
series Exp_w_neg = (lambda/(1+lambda))*(1/chi2)*(cnorm(b1)+exp(a2 - a1 - b2*sv + 0.5*(sv^2))*cnorm(b2-sv))
series Exp_u_pos = (lambda/(lambda-1))*(1/chi1)*(cnorm(b2)+exp(0.5*((b1 + sv)^2 - b2^2))*cnorm(b1+sv))
series Exp_u_neg = (lambda/(1+lambda))*(1/chi1)*(cnorm(b2)+exp(a1 - a2 - b1*sv + 0.5*(sv^2))*cnorm(b1-sv))
series ExpDens2tsf = (1/(sw+su))* (exp(a1)*cnorm(b1) + exp(a2)*cnorm(b2))
series Exp_wu = (exp( (1+su)*(a1+sv^2/(2*su)))*cnorm(b1-sv) + exp( (1-sw)*(a2-sv^2/(2*sw)))*cnorm(b2+sv)) / ((sw+su)*ExpDens2tsf)
series Exp_uw = (exp( (1-su)*(a1-sv^2/(2*su)))*cnorm(b1+sv) + exp( (1+sw)*(a2+sv^2/(2*sw)))*cnorm(b2-sv)) / ((sw+su)*ExpDens2tsf)

#metrics (those used in the application)
series M1 = 1-Exp_w_neg
series M4 = Exp_w_pos - Exp_u_neg
series M7 = Exp_wu - 1
series M10 = 1-Exp_uw
series M10plus = Exp_uw <= 1 ? 1-Exp_uw : NA
series M10minus = Exp_uw > 1 ? Exp_uw - 1 : NA

##


